#!/opt/local/bin/gnuplot

# set terminal pdf size 48cm,12cm noenhanced dashed
set terminal pdf monochrome dashed size 48cm,12cm noenhanced
set output 'hd1_arbiter_and_xor.pdf'

set style line 2 lc rgb 'black' lt 1 lw 4
set style line 3 lc rgb 'black' lt 2 lw 2
set style line 4 lc rgb 'black' lt 3 lw 2
set style line 5 lc rgb 'black' lt 4 lw 4
set style line 6 lc rgb 'black' lt 5 lw 4
set style line 7 lc rgb 'black' lt 6 lw 4
set style line 10 lc rgb 'black' lt 1 lw 2


set xrange [0:65]
set xtics  0,5,65 

set xtics nomirror
set ytics nomirror
# set grid ytics

set xtics font ", 35"
set ytics font ", 35"

set ylabel font ", 45"
set xlabel font ", 45"

set key font ",30"

set tmargin at screen 0.89
set bmargin 7.5
set lmargin 17
set ylabel offset -8,0
set xlabel offset 0,-2

set pointsize 1

set ytics  0,10,100 
set yrange [0:100]


set multiplot layout 1,2

unset key
set ylabel "Probability of same response"
set xlabel "Flipped challenge bit position"
plot 'Probability' using 1:($8*100) with lines ls 10 title "Arbiter (analytical)", \
	'' every 2:2 using 1:4 with points pt 2 title "Arbiter (empirical)", \
	'' using 1:($9*100) with lines ls 3 title "XOR-Arbiter (analytical, k=3)", \
	'' every 2:2 using 1:5 with points pt 4 title "XOR-Arbiter (empirical, k=3)",\
	'' using 1:($11*100) with lines ls 3 title "XOR-Arbiter (analytical, k=5)", \
	'' every 2:2 using 1:10 with points pt 6 title "XOR-Arbiter (empirical, k=5)"

set ylabel "Response entropy"
set xlabel "Flipped challenge bit position"

set yrange [0:1.05]
set ytics  0,1,1 
unset grid
set lmargin 15
set rmargin 5
set ylabel offset -4.5,0
# set xlabel offset 0,-2


plot "<(sed -n '1,63p' entropy)" using 1:8 with line ls 10 title "Arbiter (analytical)", \
	'entropy' every 2:2 using 1:4 with points pt 2 title "Arbiter (empirical)", \
	'' using 1:9 with line ls 3 title "XOR-Arbiter (analytical)", \
	'' every 2:2 using 1:5 with points pt 4 title "XOR-Arbiter (empirical)",\
	'' using 1:11 with lines ls 3 title "XOR-Arbiter (k=5)", \
	'' every 2:2 using 1:10 with points pt 6 title "XOR-Arbiter (k=5)"

unset title
unset xlabel
unset ylabel
unset border
unset xtics
unset ytics

set key at screen 0.20,screen 1 vertical maxrows 1 font ",33"
plot NaN title "Arbiter (analytical)" with line ls 10 

set key at screen 0.35,screen 1 vertical maxrows 1 font ",33"
set pointsize 2
plot NaN title "Arbiter (empirical)" with points pt 2

set key at screen 0.55,screen 1 vertical maxrows 1 font ",33"
plot NaN title "XOR-Arbiter (analytical)" with line ls 3 

set key at screen 0.75,screen 1 vertical maxrows 1 font ",33"
set pointsize 2
plot NaN title "XOR-Arbiter (empirical, k=3)" with points pt 4 

set key at screen .95,screen 1 vertical maxrows 1 font ",33"
set pointsize 2
plot NaN title "XOR-Arbiter (empirical, k=5)" with points pt 6 


unset multiplot

